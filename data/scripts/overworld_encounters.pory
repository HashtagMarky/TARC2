// 0x8004 Species
// 0x8005 Shiny Status
// 0x8006 Level
// 0x8007 Pokéball
// 0x8008 Emotion

script OverworldEncounters_EventScript_EncounterApproachPlayer
{
	lock
    playse(SE_PIN)
	special(DoTrainerApproach)
	waitstate
    special(GetOverworldMonSpecies)
	gotonative(LoadOverworldPokemonObjectScript)
	end
}

script OverworldEncounters_EventScript_Encounter
{
    lock
    faceplayer
    special(GetOverworldMonSpecies)
    if (var(VAR_0x8008) != MUGSHOT_EMOTE_ANGRY)
    {
        setvar(VAR_0x8008, MUGSHOT_EMOTE_SURPRISED)
    }
    bufferspeciesname(STR_VAR_1, VAR_0x8004)
    playmoncry(VAR_0x8004, CRY_MODE_ENCOUNTER)
    if (var(VAR_0x8008) != MUGSHOT_EMOTE_SURPRISED)
    {
        message(format("It's a wild {STR_VAR_1}!\n What will {PLAYER} do?"))
    }
    else
    {
        message(format("{PLAYER} caught a wild {STR_VAR_1} off-guard!\p The wild {STR_VAR_1} is panicking…\n What will {PLAYER} do?"))
    }
    if (specialvar(VAR_RESULT, GetPlayerFacingDirection) == DIR_NORTH && flag(FLAG_SYS_OVERWORLD_ENCOUNTER_CAMERA) == FALSE)
    {
        special(SpawnCameraObject)
        setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        applymovement(OBJ_EVENT_ID_CAMERA, OverworldEncounters_Movement_CameraAdjust)
        waitmovement(OBJ_EVENT_ID_CAMERA)
        setflag(FLAG_SYS_OVERWORLD_ENCOUNTER_CAMERA)
        clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        special(RemoveCameraObject)
    }
    waitmessage
    dynmultipush("{FONT_GET_NARROW}FIGHT", 0)
    if (specialvar(VAR_RESULT, HasAtLeastOnePokeBall) == TRUE)
    {
        dynmultipush("{FONT_GET_NARROWER}{POKE}BALL", 1)
    }
    if (checkitem(ITEM_POKEBLOCK_CASE) == TRUE)
    {
        dynmultipush("{FONT_GET_NARROW}APPROACH", 2)
    }
    dynmultipush("{FONT_GET_NARROW}RUN", 3)
    dynmultistack(0, 0, TRUE, 4, TRUE, 0, DYN_MULTICHOICE_CB_SHOW_PMD)
    switch (var(VAR_RESULT))
    {
        case 0:
            goto(OverworldEncounters_EventScript_Fight)
        case 1:
            goto(OverworldEncounters_EventScript_ThrowBall)
        case 2:
            goto(OverworldEncounters_EventScript_Approach)
        default:
            goto(OverworldEncounters_EventScript_RunAway)
    }
    end
}

script OverworldEncounters_EventScript_Fight
{
    createmon(B_SIDE_OPPONENT, B_FLANK_LEFT, VAR_0x8004, VAR_OVERWORLD_MON_LEVEL, isShiny=VAR_0x8005)
    closemessage
    waitmoncry
    dowildsafaribattle
    specialvar(VAR_RESULT, GetBattleOutcome)
    switch (var(VAR_RESULT))
    {
        case B_OUTCOME_WON:
        case B_OUTCOME_DREW:
            playmoncry(VAR_0x8004, CRY_MODE_FAINT)
        case B_OUTCOME_MON_FLED:
        case B_OUTCOME_MON_TELEPORTED:
            playmoncry(VAR_0x8004, CRY_MODE_DOUBLES)
        case B_OUTCOME_CAUGHT:
            playmoncry(VAR_0x8004, CRY_MODE_NORMAL)
        default:
            call(OverworldEncounters_EventScript_EncounterEscapeAttempt)
            goto(OverworldEncounters_EventScript_EncounterEnd)
    }
    goto(OverworldEncounters_EventScript_EncounterRemove)
    end
}

script OverworldEncounters_EventScript_ThrowBall
{
    setvar(VAR_0x8007, ITEM_POKE_BALL)
    while (var(VAR_0x8007) <= ITEM_CHERISH_BALL)
    {
        if (checkitem(VAR_0x8007) == TRUE)
        {
            bufferitemname(STR_VAR_1, VAR_0x8007)
            dynmultipush("{FONT_GET_NARROW}{STR_VAR_1}", VAR_0x8007)
        }
        addvar(VAR_0x8007, 1)
    }
    if (checkitem(ITEM_STRANGE_BALL) == TRUE)
    {
        bufferitemname(STR_VAR_1, ITEM_STRANGE_BALL)
        dynmultipush("{FONT_GET_NARROW}{STR_VAR_1}", ITEM_STRANGE_BALL)
    }
    message(format("What {FONT_GET_NARROWER}{POKE}BALL {FONT_NORMAL}will {PLAYER} use?"))
    waitmessage
    dynmultistack(0, 0, FALSE, 3, TRUE, 0, DYN_MULTICHOICE_CB_SHOW_ITEM)
    if (var(VAR_RESULT) == MULTI_B_PRESSED)
    {
        goto(OverworldEncounters_EventScript_Encounter)
    }
    else
    {
        copyvar(VAR_0x8007, VAR_RESULT)
        removeitem(VAR_0x8007, 1)
        goto(OverworldEncounters_EventScript_CatchAttempt)
    }
    end
}

script OverworldEncounters_EventScript_Approach
{
    special(EnterSafariMode)
    goto(OverworldEncounters_EventScript_Fight)
}

script OverworldEncounters_EventScript_CatchAttempt
{
    special(GetOverworldSpeciesCatchRate)
    bufferspeciesname(STR_VAR_1, VAR_0x8004)
    switch (var(VAR_RESULT))
    {
        case 3:
            message(format("The wild {STR_VAR_1} was caught!\n It was a critical capture!"))
        case 1:
            message(format("The wild {STR_VAR_1} was caught!"))
        default:
            message(format("The wild {STR_VAR_1} broke free!"))
            waitmessage
            waitbuttonpress
            call(OverworldEncounters_EventScript_EncounterEscapeAttempt)
            message(format("The wild {STR_VAR_1} lunged\n at {PLAYER}!"))
            playmoncry(VAR_0x8004, CRY_MODE_ENCOUNTER)
            waitmessage
            waitbuttonpress
            goto(OverworldEncounters_EventScript_Fight)
    }
    playfanfare(MUS_OBTAIN_ITEM)
    waitfanfare
    waitmessage
    waitbuttonpress
    closemessage
    givemon(VAR_0x8004, VAR_OVERWORLD_MON_LEVEL, isShiny=VAR_0x8005)
    playmoncry(VAR_0x8004, CRY_MODE_NORMAL)
    goto(OverworldEncounters_EventScript_EncounterRemove)
    end
}

script OverworldEncounters_EventScript_RunAway
{
    if (specialvar(VAR_RESULT, OverworldTrappedInBattle) == BATTLE_RUN_SUCCESS)
    {
        message(format("{PLAYER} got away from the\n wild {STR_VAR_1}!"))
        waitmessage
        waitbuttonpress
        call(OverworldEncounters_EventScript_EncounterEscapeAttempt)
        goto(OverworldEncounters_EventScript_EncounterEnd)
    }
    else
    {
        message(format("Couldn't get away from the\n wild {STR_VAR_1}!\p The wild {STR_VAR_1} lunged\n at {PLAYER}!"))
        waitmessage
        playmoncry(VAR_0x8004, CRY_MODE_ENCOUNTER)
        waitbuttonpress
        goto(OverworldEncounters_EventScript_Fight)
    }
    end
}

script OverworldEncounters_EventScript_EncounterEscapeAttempt
{
    if (specialvar(VAR_RESULT, WillOverworldEncounterRun) == TRUE)
    {
        goto(OverworldEncounters_EventScript_EncounterEscape)
    }
    return
}

script OverworldEncounters_EventScript_EncounterEscape
{
    closemessage
    special(GetOverworldMonSpecies)
    playmoncry(VAR_0x8004, CRY_MODE_DOUBLES)
    goto(OverworldEncounters_EventScript_EncounterRemove)
    end
}

script OverworldEncounters_EventScript_EncounterRemove
{
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(VAR_LAST_TALKED)
	fadescreenswapbuffers(FADE_FROM_BLACK)
    waitfanfare
    waitmoncry
    goto(OverworldEncounters_EventScript_EncounterEnd)
    end
}

script OverworldEncounters_EventScript_EncounterEnd
{
    closemessage
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceAwayPlayer)
    waitmovement(VAR_LAST_TALKED)
    if (specialvar(VAR_RESULT, GetPlayerFacingDirection) == DIR_NORTH && flag(FLAG_SYS_OVERWORLD_ENCOUNTER_CAMERA) == TRUE)
    {
        special(SpawnCameraObject)
        setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        applymovement(OBJ_EVENT_ID_CAMERA, OverworldEncounters_Movement_CameraReAdjust)
        waitmovement(OBJ_EVENT_ID_CAMERA)
        clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        special(RemoveCameraObject)
    }
    call(EventScript_DisplayPokedexHeader)
    setvar(VAR_0x8008, MUGSHOT_EMOTE_NORMAL)
    clearflag(FLAG_SYS_OVERWORLD_ENCOUNTER_CAMERA)
    special(ExitSafariMode)
    releaseall
    end
}

movement OverworldEncounters_Movement_CameraAdjust {
    walk_up * 1
}

movement OverworldEncounters_Movement_CameraReAdjust {
    walk_down * 1
}
