mapscripts VyratonOutdoors_Harbour_MapScripts
{
    MAP_SCRIPT_ON_FRAME_TABLE
    [
        VAR_IKIGAI_INTRO_STATE, 0: VyratonOutdoors_Harbour_EventScript_IkigaiIntro
    ]
}

const LOCALID_Samuel        = 2
const LOCALID_Grandfather   = 9

script VyratonOutdoors_Harbour_EventScript_IkigaiIntro
{
    setvar(VAR_IKIGAI_INTRO_STATE, 1)

    call(VyratonOutdoors_Harbour_EventScript_IkigaiIntro_Disembarkment)
    call(VyratonOutdoors_Harbour_EventScript_IkigaiIntro_Meet)
    call(VyratonOutdoors_Harbour_EventScript_IkigaiIntro_Departures)
}

script VyratonOutdoors_Harbour_EventScript_IkigaiIntro_Disembarkment
{
    call(VyratonOutdoors_Harbour_EventScript_SSPathfinder_DisembarkPassengers)
}

script VyratonOutdoors_Harbour_EventScript_IkigaiIntro_Meet
{
    speakername(known=FALSE)
    msgbox(VyratonOutdoors_Harbour_Text_ExcuseMe, MSGBOX_DEFAULT)
    closemessage
    addobject(LOCALID_Grandfather)
    applymovement(LOCALID_Grandfather, VyratonOutdoors_Harbour_Movement_Grandfather_Exit)
    applymoveplayer(VyratonOutdoors_Harbour_Movement_Player_MoveOutOfWay, FALSE)
    applymove(LOCALID_Samuel, VyratonOutdoors_Harbour_Movement_Samuel_BeginEntrance)
    turnobject(LOCALID_Samuel, DIR_NORTH)
    emote(LOCALID_Samuel, EMOTE_EXCLAM)
    delay(40)
    applymoveplayer(VyratonOutdoors_Harbour_Movement_Player_MeetSamuel, FALSE)
    applymove(LOCALID_Samuel, VyratonOutdoors_Harbour_Movement_Samuel_ContinueEntrance)
    mnmbox(VyratonOutdoors_Harbour_Text_WelcomeToVyraton, nameKnown=FALSE, localId=LOCALID_Samuel)
    turnplayer(DIR_SOUTH)
    turnobject(LOCALID_Samuel, DIR_SOUTH)
}

script VyratonOutdoors_Harbour_EventScript_IkigaiIntro_Departures
{
    call(VyratonOutdoors_Harbour_EventScript_SSPathfinder_Departure)
    turnplayer(DIR_WEST)
    mnmbox(VyratonOutdoors_Harbour_Text_HeadToOffice)
    applymoveplayer(VyratonOutdoors_Harbour_Movement_Player_Exit, FALSE)
    applymove(LOCALID_Samuel, VyratonOutdoors_Harbour_Movement_Samuel_Exit)
    warp(MAP_VYRATON_INDOORS_MAYORS_OFFICE, 4, 9)
    setflag(FLAG_HIDE_VYRATON_HARBOR_SAMUEL)
    setflag(FLAG_HIDE_VYRATON_HARBOR_GRANDFATHER)
    waitstate
}

movement VyratonOutdoors_Harbour_Movement_Grandfather_Exit
{
    delay_2
    delay_16
    walk_up * 7
}

movement VyratonOutdoors_Harbour_Movement_Player_MoveOutOfWay
{
    face_left
    lock_facing_direction
    walk_right
    unlock_facing_direction
    delay_16 * 2
    walk_left
    face_up
}

movement VyratonOutdoors_Harbour_Movement_Samuel_BeginEntrance
{
    walk_down * 4
}

movement VyratonOutdoors_Harbour_Movement_Player_MeetSamuel
{
    delay_16 * 2
    delay_8
    // delay_2
    face_left
}

movement VyratonOutdoors_Harbour_Movement_Samuel_ContinueEntrance
{
    walk_down * 2
    face_right
}

movement VyratonOutdoors_Harbour_Movement_Samuel_Exit
{
    walk_up
    walk_right
    walk_up
}

movement VyratonOutdoors_Harbour_Movement_Player_Exit
{
    delay_16 * 2
    walk_up
}