mapscripts VyratonIndoors_MayorsOffice_MapScripts
{
    MAP_SCRIPT_ON_FRAME_TABLE
    [
        VAR_IKIGAI_INTRO_STATE, 1: VyratonIndoors_MayorsOffice_EventScript_IkigaiIntro
    ]
}

const LOCALID_Samuel = 1
const LOCALID_Starter = 2

script VyratonIndoors_MayorsOffice_EventScript_IkigaiIntro_DontLeave
{
    playerbox(VyratonIndoors_MayorsOffice_Text_ShouldReturn, EMOTE_THINK)
    applymoveplayer(Common_Movement_WalkUp)
}

script VyratonIndoors_MayorsOffice_EventScript_IkigaiIntro
{
    setvar(VAR_IKIGAI_INTRO_STATE, 2)

    call(VyratonIndoors_MayorsOffice_EventScript_IkigaiIntro_EnterOffice)
}

script VyratonIndoors_MayorsOffice_EventScript_IkigaiIntro_EnterOffice
{
    applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_Entrance)
    mnmbox(VyratonIndoors_MayorsOffice_Text_KickStart, localId=LOCALID_Samuel)
}

script VyratonIndoors_MayorsOffice_EventScript_Samuel
{
    if (flag(FLAG_SYS_POKEDEX_GET))
    {
        call(VyratonIndoors_MayorsOffice_EventScript_Samuel_GoMeetMon)
    }
    else
    {
        call(VyratonIndoors_MayorsOffice_EventScript_Samuel_OpenBriefcase)
    }
}

script VyratonIndoors_MayorsOffice_EventScript_Samuel_OpenBriefcase
{
    mnmbox(VyratonIndoors_MayorsOffice_Text_CrackOpen)
}

script VyratonIndoors_MayorsOffice_EventScript_Samuel_GoMeetMon
{
    mnmbox(VyratonIndoors_MayorsOffice_Text_GoMeetMon)
}

script VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase
{
    if (flag(FLAG_SYS_POKEMON_GET))
    {
        call(VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_MeetPartner)
    }
    else
    {
        call(VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_OpenBriefcase)
        call(VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_TakePokedex)
    }
}

script VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_OpenBriefcase
{
    if (specialvar(VAR_RESULT, GetPlayerFacingDirection) == DIR_WEST)
    {
        applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromBriefcase)
        turnplayer(DIR_NORTH)
    }
    callnative(StartNewPokeballCaseUI)
    setflag(FLAG_SYS_POKEMON_GET)
    waitstate
    playmoncry(VAR_STARTER_MON, CRY_MODE_NORMAL)
    addobject(LOCALID_Starter)
    applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromBriefcase, FALSE)
    applymove(LOCALID_Starter, VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromBriefcase)
    turnplayer(DIR_NORTH)
}

script VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_TakePokedex
{
    setflag(FLAG_SYS_POKEDEX_GET)
	setflag(FLAG_SYS_NATIONAL_DEX)
	special(EnableNationalPokedex)

    bufferpartymonnick(STR_VAR_1, 0)
    if (specialvar(VAR_RESULT, IsLeadMonNicknamedOrNotEnglish) == TRUE)
    {
        mnmbox(VyratonIndoors_MayorsOffice_Text_MeetNicknamedMon, localId=LOCALID_Samuel)
    }
    else
    {
        mnmbox(VyratonIndoors_MayorsOffice_Text_MeetNonNicknamedMon, localId=LOCALID_Samuel)
    }
    turnplayer(DIR_EAST)
}

script VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_MeetPartner
{
    bufferpartymonnick(STR_VAR_1, 0)
    mnmbox(VyratonIndoors_MayorsOffice_Text_ExtremelySet, localId=LOCALID_Samuel)
}

script VyratonIndoors_MayorsOffice_EventScript_StarterMon
{
    clearflag(FLAG_SUPPRESS_MUGSHOT)
    call(VyratonIndoors_MayorsOffice_EventScript_StarterMon_DeterminedMon)
    setflag(FLAG_SUPPRESS_MUGSHOT)
    call(VyratonIndoors_MayorsOffice_EventScript_StarterMon_MetStarter)
}

script VyratonIndoors_MayorsOffice_EventScript_StarterMon_DeterminedMon
{
    bufferpartymonnick(STR_VAR_1, 0)
    special(GetOverworldMonSpecies)
    callnative(ClearPokedexFlags)
    callnative(ShowPokedexHeaderMessage)
    playfanfare(MUS_HG_RECEIVE_POKEMON)
    faceplayer
    createfieldmugshot(MUGSHOT_PMD, emotion=EMOTE_DETERMINED)
    special(GetOverworldMonSpecies)
    call(EventScript_DisplayPokedexSpeciesName)
    playmoncry(VAR_0x8004, CRY_MODE_NORMAL)
    message(VyratonIndoors_MayorsOffice_Text_DeterminedMon)
    setseenmon(VAR_0x8004)
    setcaughtmon(VAR_0x8004)
    waitmessage
    waitbuttonpress
    waitfanfare
    closemessage
}

script VyratonIndoors_MayorsOffice_EventScript_StarterMon_MetStarter
{
    turnobject(LOCALID_Starter, DIR_NORTH)
    if (specialvar(VAR_RESULT, GetPlayerFacingDirection) == DIR_WEST)
    {
        applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromStarter_One)
    }
    elif (specialvar(VAR_RESULT, GetPlayerFacingDirection) == DIR_SOUTH)
    {
        applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromStarter_Two)
    }
    turnplayer(DIR_NORTH)
}


movement VyratonIndoors_MayorsOffice_Movement_Player_Entrance
{
    walk_up
    walk_left
    walk_up * 3
    walk_right
    face_up
}

movement VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromBriefcase
{
    walk_down
    walk_left
}

movement VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromStarter_One
{
    walk_up
    walk_left * 2
    walk_down
}

movement VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromStarter_Two
{
    walk_left
    walk_down
}