mapscripts VyratonIndoors_MayorsOffice_MapScripts
{
    MAP_SCRIPT_ON_FRAME_TABLE
    [
        VAR_IKIGAI_INTRO_STATE, 3: VyratonIndoors_MayorsOffice_EventScript_IkigaiIntro
    ]
}

const LOCALID_Samuel = 1
const LOCALID_Starter = 2
const LOCALID_Thomas = 4

script VyratonIndoors_MayorsOffice_EventScript_IkigaiIntro_DontLeave
{
    playerbox(VyratonIndoors_MayorsOffice_Text_ShouldReturn, EMOTE_THINK)
    applymoveplayer(Common_Movement_WalkUp)
    end
}

script VyratonIndoors_MayorsOffice_EventScript_IkigaiIntro
{
    setvar(VAR_IKIGAI_INTRO_STATE, 4)

    call(VyratonIndoors_MayorsOffice_EventScript_IkigaiIntro_EnterOffice)
}

script VyratonIndoors_MayorsOffice_EventScript_IkigaiIntro_EnterOffice
{
    applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_Entrance)
    mnmbox(VyratonIndoors_MayorsOffice_Text_KickStart, localId=LOCALID_Samuel)
    end
}

script VyratonIndoors_MayorsOffice_EventScript_Samuel
{
    if (flag(FLAG_SYS_POKEDEX_GET))
    {
        call(VyratonIndoors_MayorsOffice_EventScript_Samuel_GoMeetMon)
    }
    else
    {
        call(VyratonIndoors_MayorsOffice_EventScript_Samuel_OpenBriefcase)
    }
    applymove(LOCALID_Samuel, Common_Movement_FaceOriginalDirection)
    end
}

script VyratonIndoors_MayorsOffice_EventScript_Samuel_OpenBriefcase
{
    mnmbox(VyratonIndoors_MayorsOffice_Text_CrackOpen)
}

script VyratonIndoors_MayorsOffice_EventScript_Samuel_GoMeetMon
{
    bufferpartymonnick(STR_VAR_1, 0)
    mnmbox(VyratonIndoors_MayorsOffice_Text_GoMeetMon)
}

script VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase
{
    if (flag(FLAG_SYS_POKEMON_GET))
    {
        call(VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_MeetPartner)
    }
    else
    {
        call(VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_OpenBriefcase)
        call(VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_TakePokedex)
    }
    applymove(LOCALID_Samuel, Common_Movement_FaceOriginalDirection)
    end
}

script VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_OpenBriefcase
{
    if (specialvar(VAR_RESULT, GetPlayerFacingDirection) == DIR_WEST)
    {
        applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_GoToBriefcase)
        turnplayer(DIR_NORTH)
    }
    callnative(StartNewPokeballCaseUI)
    setflag(FLAG_SYS_POKEMON_GET)
    waitstate
    playmoncry(VAR_STARTER_MON, CRY_MODE_NORMAL)
    addobject(LOCALID_Starter)
    applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromBriefcase, FALSE)
    applymove(LOCALID_Starter, VyratonIndoors_MayorsOffice_Movement_Starter_ReturnFromBriefcase)
    turnplayer(DIR_NORTH)
    releaseobjects
}

script VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_TakePokedex
{
    setflag(FLAG_SYS_POKEDEX_GET)
	setflag(FLAG_SYS_NATIONAL_DEX)
	special(EnableNationalPokedex)

    bufferpartymonnick(STR_VAR_1, 0)
    setvar(VAR_0x8000, ITEM_POKEDEX)
    if (specialvar(VAR_RESULT, IsLeadMonNicknamedOrNotEnglish) == TRUE)
    {
        mnmbox(VyratonIndoors_MayorsOffice_Text_MeetNicknamedMon, localId=LOCALID_Samuel, lock=FALSE)
    }
    else
    {
        mnmbox(VyratonIndoors_MayorsOffice_Text_MeetNonNicknamedMon, localId=LOCALID_Samuel, lock=FALSE)
    }
    call(VyratonIndoors_MayorsOffice_EventScript_MeetThomas)
    call(VyratonIndoors_MayorsOffice_EventScript_Samuel_ReceivePokdex)
    turnplayer(DIR_EAST)
}

script VyratonIndoors_MayorsOffice_EventScript_SamuelBriefcase_MeetPartner
{
    bufferpartymonnick(STR_VAR_1, 0)
    mnmbox(VyratonIndoors_MayorsOffice_Text_ExtremelySet, localId=LOCALID_Samuel)
}

script VyratonIndoors_MayorsOffice_EventScript_StarterMon
{
    clearflag(FLAG_SUPPRESS_MUGSHOT)
    call(VyratonIndoors_MayorsOffice_EventScript_StarterMon_DeterminedMon)
    setflag(FLAG_SUPPRESS_MUGSHOT)
    call(VyratonIndoors_MayorsOffice_EventScript_StarterMon_MetStarter)
    call(VyratonIndoors_MayorsOffice_EventScript_StarterMon_OtherImportantThings)
    end
}

script VyratonIndoors_MayorsOffice_EventScript_StarterMon_DeterminedMon
{
    bufferpartymonnick(STR_VAR_1, 0)
    special(GetOverworldMonSpecies)
    callnative(ClearPokedexFlags)
    callnative(ShowPokedexHeaderMessage)
    playfanfare(MUS_HG_RECEIVE_POKEMON)
    faceplayer
    createfieldmugshot(MUGSHOT_PMD, emotion=EMOTE_DETERMINED)
    special(GetOverworldMonSpecies)
    call(EventScript_DisplayPokedexSpeciesName)
    playmoncry(VAR_0x8004, CRY_MODE_NORMAL)
    message(VyratonIndoors_MayorsOffice_Text_DeterminedMon)
    callnative(SetIkigaiStarterPokedexFlags)
    setcaughtmon(VAR_0x8004)
    waitmessage
    waitbuttonpress
    waitfanfare
    closemessage
    delay(40)
}

script VyratonIndoors_MayorsOffice_EventScript_StarterMon_MetStarter
{
    applymove(LOCALID_Starter, Common_Movement_FaceUp)
    releaseobjects
    if (specialvar(VAR_RESULT, GetPlayerFacingDirection) == DIR_WEST)
    {
        applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromStarter_One)
    }
    elif (specialvar(VAR_RESULT, GetPlayerFacingDirection) == DIR_SOUTH)
    {
        applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromStarter_Two)
    }
    turnplayer(DIR_NORTH)
}

script VyratonIndoors_MayorsOffice_EventScript_StarterMon_OtherImportantThings
{
    // setvar(VAR_0x8000, ITEM_TM_RETURN)
    bufferpartymonnick(STR_VAR_1, 0)
    mnmbox(VyratonIndoors_MayorsOffice_Text_OtherImportantThings, localId=LOCALID_Samuel)
}

script VyratonIndoors_MayorsOffice_EventScript_MeetThomas
{
    fadenewbgm(MUS_HG_POKEWALKER)
    delay(60)
    emote(LOCALID_Samuel, EMOTE_EXCLAM, wait=FALSE)
    turnplayer(DIR_SOUTH)
    addobject(LOCALID_Thomas)
    applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_WatchThomasEntrance, FALSE)
    applymove(LOCALID_Thomas, VyratonIndoors_MayorsOffice_Movement_Thomas_Entrance)
    turnobject(LOCALID_Samuel, DIR_WEST)
    speakername(known=FALSE)
    msgbox(VyratonIndoors_MayorsOffice_Text_YouForgotThis, MSGBOX_DEFAULT)
    closemessage
    turnobject(LOCALID_Thomas, DIR_SOUTH)
    emote(LOCALID_Thomas, EMOTE_QUESTION)
    turnobject(LOCALID_Samuel, DIR_SOUTH)
    speakername(known=FALSE)
    msgbox(VyratonIndoors_MayorsOffice_Text_IsThisThem, MSGBOX_DEFAULT)
    closemessage
    applymoveplayer(VyratonIndoors_MayorsOffice_Movement_Player_WatchThomasExit, FALSE)
    applymove(LOCALID_Thomas, VyratonIndoors_MayorsOffice_Movement_Thomas_Exit)
    removeobject(LOCALID_Thomas)
    fadedefaultbgm
    delay(60)
    turnplayer(DIR_NORTH)
}

script VyratonIndoors_MayorsOffice_EventScript_Samuel_ReceivePokdex
{
    mnmbox(VyratonIndoors_MayorsOffice_Text_GivePokedex, lock=FALSE)
}


movement VyratonIndoors_MayorsOffice_Movement_Player_Entrance
{
    walk_up
    walk_left
    walk_up * 3
    walk_right
    face_up
}

movement VyratonIndoors_MayorsOffice_Movement_Player_GoToBriefcase
{
    walk_down
    walk_left
}

movement VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromBriefcase
{
    delay_16
    walk_down
    walk_left
}

movement VyratonIndoors_MayorsOffice_Movement_Starter_ReturnFromBriefcase
{
    exit_pokeball
    walk_down
    walk_left
}

movement VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromStarter_One
{
    walk_up
    walk_left * 2
    walk_down
}

movement VyratonIndoors_MayorsOffice_Movement_Player_ReturnFromStarter_Two
{
    walk_left
    walk_down
}

movement VyratonIndoors_MayorsOffice_Movement_Thomas_Entrance
{
    delay_16
    walk_up
    walk_left
    walk_up * 5
    face_right
}

movement VyratonIndoors_MayorsOffice_Movement_Thomas_Exit
{
    walk_down * 5
    walk_right
    walk_down
    delay_16
    set_invisible
}

movement VyratonIndoors_MayorsOffice_Movement_Player_WatchThomasEntrance
{
    emote_exclamation_mark
    delay_16 * 5
    delay_8
    face_left
    delay_2
    delay_4
    delay_8
    face_up
}

movement VyratonIndoors_MayorsOffice_Movement_Player_WatchThomasExit
{
    delay_16
    delay_8
    face_left
    delay_2
    delay_4
    delay_8
    face_down
}